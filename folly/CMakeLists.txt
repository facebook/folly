# Copyright (c) Meta Platforms, Inc. and affiliates.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.


add_library(
    follybenchmark
    Benchmark.cpp
)
set_property(TARGET follybenchmark PROPERTY VERSION ${PACKAGE_VERSION})
target_compile_definitions(follybenchmark PRIVATE BOOST_NO_AUTO_PTR)
target_link_libraries(follybenchmark PUBLIC folly)
apply_folly_compile_options_to_target(follybenchmark)
install(
  TARGETS follybenchmark
  EXPORT folly
  RUNTIME DESTINATION ${BIN_INSTALL_DIR}
  LIBRARY DESTINATION ${LIB_INSTALL_DIR}
  ARCHIVE DESTINATION ${LIB_INSTALL_DIR}
)

add_subdirectory(debugging/exception_tracer)
add_subdirectory(logging/example)

if (PYTHON_EXTENSIONS)
  # Create tree of symbolic links in structure required for successful
  # compliation by Cython.
  #   - must be in path named same as extension

  set(_cybld "${CMAKE_CURRENT_BINARY_DIR}/cybld")

  add_custom_target(create_binding_symlink ALL)
  file(GLOB BindingFiles
    "${CMAKE_CURRENT_SOURCE_DIR}/python/executor.pyx"
    "${CMAKE_CURRENT_SOURCE_DIR}/python/iobuf.pyx"
    "${CMAKE_CURRENT_SOURCE_DIR}/python/iobuf_ext.cpp"
    "${CMAKE_CURRENT_SOURCE_DIR}/python/ProactorExecutor.cpp"
    "${CMAKE_CURRENT_SOURCE_DIR}/python/*.pxd"
    "${CMAKE_CURRENT_SOURCE_DIR}/python/*.h"
  )
  file(MAKE_DIRECTORY "${_cybld}/folly/")
  file(MAKE_DIRECTORY "${_cybld}/folly/python/")
  # Create __init__.py for folly package (required for Python to recognize it as a package)
  file(WRITE "${_cybld}/folly/__init__.py" "# Auto-generated by CMake\n")

  foreach(_src ${BindingFiles})
    get_filename_component(_target_file "${_src}" NAME)

    message(
      STATUS
      "Linking ${_src} "
      "to ${_cybld}/folly/${_target_file}"
    )
    add_custom_command(TARGET create_binding_symlink PRE_BUILD
      COMMAND
        ${CMAKE_COMMAND} -E create_symlink
        "${_src}"
        "${_cybld}/folly/${_target_file}"
    )
  endforeach()

  # Tell setup.py where to find includes and libfolly.so
  set(prop "$<TARGET_PROPERTY:folly_base,INCLUDE_DIRECTORIES>")
  set(incs "$<$<BOOL:${prop}>:-I$<JOIN:${prop},:>>")
  set(glog_lib "${GLOG_LIBRARY}")
  cmake_path(REMOVE_FILENAME glog_lib)
  set(libs "-L${CMAKE_CURRENT_BINARY_DIR}:${CMAKE_BINARY_DIR}:${glog_lib}")

  # Generate API headers only (no linking)
  add_custom_command(
    OUTPUT ${_cybld}/folly/iobuf_api.h
    COMMAND python3 ${CMAKE_CURRENT_SOURCE_DIR}/python/setup.py --api-only
    DEPENDS create_binding_symlink
    WORKING_DIRECTORY ${_cybld}
  )

  add_custom_target(folly_python_api_headers ALL
    DEPENDS ${_cybld}/folly/iobuf_api.h
  )

  add_custom_command(
    OUTPUT ${_cybld}/folly/python/iobuf_api.h
    COMMAND
      ${CMAKE_COMMAND} -E create_symlink
      "${_cybld}/folly/iobuf_api.h"
      "${_cybld}/folly/python/iobuf_api.h"
    DEPENDS ${_cybld}/folly/iobuf_api.h
  )

  add_custom_target(create_post_binding_symlink ALL
    DEPENDS ${_cybld}/folly/python/iobuf_api.h
  )

  add_library(
    folly_python_cpp
      python/AsyncioExecutor.cpp
      python/error.cpp
      python/executor.cpp
      python/iobuf.cpp
  )

  target_include_directories(folly_python_cpp PRIVATE ${Python3_INCLUDE_DIRS})
  target_link_libraries(folly_python_cpp PRIVATE Python3::Python)
  add_dependencies(folly_python_cpp folly create_post_binding_symlink)
  set_property(TARGET folly_python_cpp PROPERTY VERSION ${PACKAGE_VERSION})
  target_compile_definitions(folly_python_cpp PRIVATE BOOST_NO_AUTO_PTR)
  target_include_directories(folly_python_cpp PRIVATE "${_cybld}")
  target_link_libraries(folly_python_cpp PUBLIC folly)
  apply_folly_compile_options_to_target(folly_python_cpp)

  # Build Python extensions after C++ library is ready
  add_custom_target(folly_python_bindings ALL
    DEPENDS folly_python_cpp folly_python_api_headers
    WORKING_DIRECTORY ${_cybld}
  )

  add_custom_command(TARGET folly_python_bindings POST_BUILD
    COMMAND ${CMAKE_COMMAND} -E env
      "CFLAGS=${CMAKE_C_FLAGS}"
      "CXXFLAGS=${CMAKE_CXX_FLAGS}"
      "CC=${CMAKE_C_COMPILER}"
      "CXX=${CMAKE_CXX_COMPILER}"
      python3 ${CMAKE_CURRENT_SOURCE_DIR}/python/setup.py
      build_ext -f ${incs} ${libs}
    WORKING_DIRECTORY ${_cybld}
  )

  install(
    TARGETS folly_python_cpp
    EXPORT folly
    DESTINATION ${LIB_INSTALL_DIR}
  )
  install(
    DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
    DESTINATION ${INCLUDE_INSTALL_DIR}
    FILES_MATCHING
      PATTERN "*.h"
  )
  # Install .pxd files for downstream Cython code that imports folly
  # (e.g., thrift uses "from folly.iobuf cimport IOBuf").
  # Flatten python/ subdirectory to match Cython's import structure:
  # folly/python/iobuf.pxd -> include/folly/iobuf.pxd
  install(
    DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/python/
    DESTINATION ${INCLUDE_INSTALL_DIR}/folly/
    FILES_MATCHING
      PATTERN "*.pxd"
  )

  add_custom_command(TARGET folly_python_bindings
    COMMAND ${CMAKE_COMMAND} -E env
      "CFLAGS=${CMAKE_C_FLAGS}"
      "CXXFLAGS=${CMAKE_CXX_FLAGS}"
      "CC=${CMAKE_C_COMPILER}"
      "CXX=${CMAKE_CXX_COMPILER}"
      python3 ${CMAKE_CURRENT_SOURCE_DIR}/python/setup.py
      bdist_wheel
    WORKING_DIRECTORY ${_cybld}
  )

  install(
    FILES
      ${_cybld}/folly/iobuf_api.h
    DESTINATION ${INCLUDE_INSTALL_DIR}/folly/python
    COMPONENT dev
  )

  # Install Folly Python bindings. getdeps.py does not allow sufficient
  # control of CMAKE_INSTALL_PREFIX, so PYTHON_PACKAGE_INSTALL_DIR can
  # be used to control the installation location of Python packages.
  set(INSTALL_DIR ${CMAKE_INSTALL_PREFIX})
  if (NOT ${PYTHON_PACKAGE_INSTALL_DIR} STREQUAL "")
    set(INSTALL_DIR ${PYTHON_PACKAGE_INSTALL_DIR})
  endif()

  # Install the wheel built by bdist_wheel above.
  # Using pip install on the wheel instead of setup.py install because:
  # - setup.py install creates eggs (zip archives) which don't work reliably
  #   in custom install directories that don't support .pth files
  # - pip install extracts files directly to site-packages/folly/, creating
  #   a proper directory structure that Python can import normally
  # --no-deps: folly has no Python dependencies
  install(CODE "
    file(GLOB _folly_wheel \"${_cybld}/dist/folly-*.whl\")
    if(_folly_wheel)
      execute_process(
        COMMAND python3 -m pip install \${_folly_wheel}
          --prefix ${INSTALL_DIR}
          --no-deps
          --force-reinstall
        COMMAND_ECHO STDOUT
      )
    else()
      message(FATAL_ERROR \"No folly wheel found in ${_cybld}/dist/\")
    endif()
  ")
endif ()
